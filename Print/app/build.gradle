apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version '1.0.0'

android {

    compileSdkVersion 25
    buildToolsVersion '25.0.2'

    defaultConfig {

        minSdkVersion 17
        targetSdkVersion 25
        versionCode 1
        versionName version
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    buildTypes {

        release {

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {

        main.java.srcDirs += 'src/main/kotlin'
    }

    packagingOptions {

        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/LICENSE.txt'
    }
}

dependencies {

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'

    compile 'com.android.support:appcompat-v7:25.1.0'

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

group = 'com.github.moraisigor'

install {

    repositories.mavenInstaller {

        pom {

            project {

                packaging 'aar'

                name 'SlidingDrawer'
                description 'SlidingDrawer hides content out of the screen and allows the user to drag a handle to bring the content on screen.'
                url 'https://github.com/MoraisIgor/SlidingDrawer'

                licenses {

                    license {

                        name 'Apache Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {

                    developer {

                        id 'MoraisIgor'
                        name 'Igor Morais'
                        email 'igor.morais@hollowsoft.com'
                    }
                }

                scm {

                    url 'https://github.com/MoraisIgor/SlidingDrawer'
                    connection 'https://github.com/MoraisIgor/SlidingDrawer.git'
                    developerConnection 'https://github.com/MoraisIgor/SlidingDrawer.git'
                }
            }
        }
    }
}

task javadoc(type: Javadoc) {

    source = android.sourceSets.main.java.srcDirs

    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {

    classifier = 'javadoc'

    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {

    from android.sourceSets.main.java.srcDirs

    classifier = 'sources'
}

artifacts {

    archives javadocJar
    archives sourcesJar
}

bintray {

    Properties properties = new Properties()

    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.key')

    configurations = ['archives']

    publish = true

    pkg {

        repo = 'Maven'

        name = 'SlidingDrawer'
        desc = 'SlidingDrawer hides content out of the screen and allows the user to drag a handle to bring the content on screen.'
        websiteUrl = 'https://github.com/MoraisIgor/SlidingDrawer'
        issueTrackerUrl = 'https://github.com/MoraisIgor/SlidingDrawer/issues'
        vcsUrl = 'https://github.com/MoraisIgor/SlidingDrawer.git'
        licenses = ['Apache-2.0']
        labels = ['android']

        version {

            gpg {

                sign = true
                passphrase = properties.getProperty('bintray.password')
            }

            /* mavenCentralSync {
                sync = true
                user = properties.getProperty('sonatype.user')
                password = properties.getProperty('sonatype.password')
                close = '1'
            } */
        }
    }
}
